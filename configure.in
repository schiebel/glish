

AC_DEFUN(AC_GLISH_INIT,
[sinclude(acsite.m4)dnl
sinclude(./aclocal.m4)dnl
AC_REQUIRE([AC_INIT_BINSH])dnl
AC_INIT_NOTICE
AC_DIVERT_POP()dnl to NORMAL
AC_DIVERT_PUSH(AC_DIVERSION_INIT)dnl
AC_INIT_PARSE_ARGS
define(AC_FD_MSG, 6)dnl
AC_CONFIG_AUX_DIR(config)

if test -z "$ARCH"; then
    AC_CANONICAL_HOST()
    ARCH="$host"
fi

##
## export needed for expanding ".../$ARCH/config.h"
##
export ARCH

if test ! -d ./$ARCH; then
    mkdir ./$ARCH || AC_MSG_ERROR( cannot create ./$ARCH )
fi

cache_file=./$ARCH/config.cache
CONFIG_STATUS=./$ARCH/config.status
CONFIG_LOG=./$ARCH/config.log

AC_GLISH_INIT_PREPARE($1)dnl

: ${CONFIG_LOG=./config.log}
dnl ######## From AC_INIT_PREPARE ########
exec AC_FD_CC>$CONFIG_LOG
echo "\
This file contains any messages produced by compilers while
running configure, to aid debugging if configure makes a mistake.
" 1>&AC_FD_CC
dnl ######## From AC_INIT_PREPARE ########

AC_DIVERT_POP()dnl to NORMAL
])

AC_DEFUN(AC_GLISH_INIT_PREPARE,
[trap 'rm -fr conftest* confdefs* core core.* *.core $ac_clean_files; exit 1' 1 2 15

# File descriptor usage:
# 0 standard input
# 1 file creation
# 2 errors and warnings
# 3 some systems may open it to /dev/tty
# 4 used on the Kubota Titan
define(AC_FD_MSG, 6)dnl
[#] AC_FD_MSG checking for... messages and results
define(AC_FD_CC, 5)dnl
[#] AC_FD_CC compiler messages saved in config.log
if test "$silent" = yes; then
  exec AC_FD_MSG>/dev/null
else
  exec AC_FD_MSG>&1
fi

# Strip out --no-create and --no-recursion so they do not pile up.
# Also quote any args containing shell metacharacters.
ac_configure_args=
for ac_arg
do
  case "$ac_arg" in
  -no-create | --no-create | --no-creat | --no-crea | --no-cre \
  | --no-cr | --no-c) ;;
  -no-recursion | --no-recursion | --no-recursio | --no-recursi \
  | --no-recurs | --no-recur | --no-recu | --no-rec | --no-re | --no-r) ;;
changequote(<<, >>)dnl
dnl If you change this globbing pattern, test it on an old shell --
dnl it's sensitive.  Putting any kind of quote in it causes syntax errors.
  *" "*|*"	"*|*[\[\]\~\<<#>>\$\^\&\*\(\)\{\}\\\|\;\<\>\?]*)
  ac_configure_args="$ac_configure_args '$ac_arg'" ;;
changequote([, ])dnl
  *) ac_configure_args="$ac_configure_args $ac_arg" ;;
  esac
done

# NLS nuisances.
# Only set LANG and LC_ALL to C if already set.
# These must not be set unconditionally because not all systems understand
# e.g. LANG=C (notably SCO).
if test "${LC_ALL+set}" = set; then LC_ALL=C; export LC_ALL; fi
if test "${LANG+set}"   = set; then LANG=C;   export LANG;   fi

# confdefs.h avoids OS command line length limits that DEFS can exceed.
rm -rf conftest* confdefs.h
# AIX cpp loses on an empty file, so make sure it contains at least a newline.
echo > confdefs.h

# A filename unique to this package, relative to the directory that
# configure is in, which we can look for to find out if srcdir is correct.
ac_unique_file=$1

# Find the source files, if location was not specified.
if test -z "$srcdir"; then
  ac_srcdir_defaulted=yes
  # Try the directory containing this script, then its parent.
  ac_prog=[$]0
changequote(, )dnl
  ac_confdir=`echo $ac_prog|sed 's%/[^/][^/]*$%%'`
changequote([, ])dnl
  test "x$ac_confdir" = "x$ac_prog" && ac_confdir=.
  srcdir=$ac_confdir
  if test ! -r $srcdir/$ac_unique_file; then
    srcdir=..
  fi
else
  ac_srcdir_defaulted=no
fi
if test ! -r $srcdir/$ac_unique_file; then
  if test "$ac_srcdir_defaulted" = yes; then
    AC_MSG_ERROR(can not find sources in $ac_confdir or ..)
  else
    AC_MSG_ERROR(can not find sources in $srcdir)
  fi
fi
dnl Double slashes in pathnames in object file debugging info
dnl mess up M-x gdb in Emacs.
changequote(, )dnl
srcdir=`echo "${srcdir}" | sed 's%\([^/]\)/*$%\1%'`
changequote([, ])dnl

dnl Let the site file select an alternate cache file if it wants to.
AC_SITE_LOAD
AC_CACHE_LOAD
AC_LANG_C
AC_PROG_ECHO_N
dnl Substitute for predefined variables.
AC_SUBST(CFLAGS)dnl
AC_SUBST(CPPFLAGS)dnl
AC_SUBST(CXXFLAGS)dnl
AC_SUBST(DEFS)dnl
AC_SUBST(LDFLAGS)dnl
AC_SUBST(LIBS)dnl
AC_SUBST(exec_prefix)dnl
AC_SUBST(prefix)dnl
AC_SUBST(program_transform_name)dnl
])

AC_GLISH_INIT(glish)

if test -z "$GLISH_HOME"; then
   if test "x$prefix" = xNONE; then
       GLISH_HOME="`pwd`"
    else
       GLISH_HOME="$prefix"
    fi
fi
AC_SUBST(GLISH_HOME)

if test -z "$NO_AUTH" && test -d npd && test -f npd/config.h.in; then
    GLISH_FLAGS="$GLISH_FLAGS -DAUTHENTICATE"
    GLISH_NPD="GLISH_NPD"
    AC_CONFIG_HEADER(glish/$ARCH/config.h:glish/config.h.in npd/$ARCH/config.h:npd/config.h.in)
    if test -z "$KEYDIR"; then
        KEYDIR="$GLISH_HOME/keys"
    fi
    AC_SUBST(KEYDIR)
    if test ! -d "$KEYDIR"; then
	mkdir "$KEYDIR"
	chmod 755 "$KEYDIR"
    fi
    if test ! -d "$KEYDIR/users"; then
	mkdir "$KEYDIR/users"
	chmod 770 "$KEYDIR/users"
    fi
    if test ! -d "$KEYDIR/hosts"; then
	mkdir "$KEYDIR/hosts"
	chmod 750 "$KEYDIR/hosts"
    fi
else
    GLISH_NPD=""
    AC_CONFIG_HEADER(glish/$ARCH/config.h)
fi

    
dnl Checks for programs.
AC_PROG_CC
AC_C_CONST
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_CHECK_PROG(RSH,remsh,remsh,rsh)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(libc.h sys/filio.h X11/fd.h sys/select.h sigLib.h unistd.h)
AC_CHECK_HEADERS(sys/signal.h vfork.h)
AC_CHECK_HEADERS(sys/time.h)
AC_CHECK_HEADERS(stdlib.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T
AC_CHECK_TYPE(malloc_t,void*)

dnl Checks for library functions.
AC_CHECK_LIB(bsd, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(inet, main)
AC_CHECK_LIB(nsl, main)
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_FUNC_VFORK
AC_CHECK_FUNC(random,AC_DEFINE(HAVE_RANDOM))
AC_CHECK_FUNC(lrand48,AC_DEFINE(HAVE_LRAND48))
AC_CHECK_FUNC(flock,AC_DEFINE(HAVE_FLOCK))
AC_CHECK_FUNC(lockf,AC_DEFINE(HAVE_LOCKF))
AC_CHECK_FUNCS(gethostname setrlimit waitpid strdup setsockopt)
AC_CHECK_FUNCS(sigprocmask uname)

if test -z "$NO_TK" && test -d rivet && test -f rivet/configure; then
    #--------------------------------------------------------------------
    #	Stolen from the Tk configure.in...
    #
    #	Locate the X11 header files and the X11 library archive.  Try
    #	the ac_path_x macro first, but if it doesn't find the X stuff
    #	(e.g. because there's no xmkmf program) then check through
    #	a list of possible directories.  Under some conditions the
    #	autoconf macro will return an include directory that contains
    #	no include files, so double-check its result just to be safe.
    #--------------------------------------------------------------------
    AC_PATH_X
    not_really_there=""
    if test "$no_x" = ""; then
        if test "$x_includes" = ""; then
    	AC_TRY_CPP([#include <X11/XIntrinsic.h>], , not_really_there="yes")
        else
    	if test ! -r $x_includes/X11/Intrinsic.h; then
    	    not_really_there="yes"
    	fi
        fi
    fi
    
    if test "$no_x" = "yes" -o "$not_really_there" = "yes"; then
        echo checking for X11 header files
        XINCLUDES="# no special path needed"
        AC_TRY_CPP([#include <X11/Intrinsic.h>], , XINCLUDES="nope")
        if test "$XINCLUDES" = nope; then
            dirs="/usr/unsupported/include /usr/local/include /usr/X386/include /usr/include/X11R4 /usr/X11R5/include /usr/include/X11R5 /usr/openwin/include /usr/X11/include /usr/sww/include"
            for i in $dirs ; do
    	    if test -r $i/X11/Intrinsic.h; then
    	        XINCLUDES=" -I$i"
    	    fi
            done
        fi
    else
        if test "$x_includes" != ""; then
    	XINCLUDES=-I$x_includes
        else
    	XINCLUDES="# no special path needed"
        fi
    fi
    if test "$XINCLUDES" = nope; then
      echo "Warning:  couldn't find any X11 include files."
      XINCLUDES="# no include files found"
    fi
    
    if test "$no_x" = yes; then
        XLIBSW=nope
        if test "$XLIBSW" = nope; then
    	dirs="/usr/unsupported/lib /usr/local/lib /usr/X386/lib /usr/lib/X11R4 /usr/X11R5/lib /usr/lib/X11R5 /usr/openwin/lib /usr/X11/lib /usr/sww/X11/lib"
    	for i in $dirs ; do
    	    if test -r $i/libX11.a; then
    		XLIBSW="-L$i -lX11"
    	    fi
    	done
        fi
    else
        if test "$x_libraries" = ""; then
    	XLIBSW=-lX11
        else
    	XLIBSW="-L$x_libraries -lX11"
        fi
    fi
    if test "$XLIBSW" = nope ; then
        AC_CHECK_LIB(Xwindow, XCreateWindow, XLIBSW=-lXwindow)
    fi
    if test "$XLIBSW" = nope ; then
        echo "Warning:  couldn't find the X11 library archive.  Using -lX11."
        XLIBSW=-lX11
    fi
    #--------------------------------------------------------------------
    GLISH_FLAGS="$GLISH_FLAGS -DGLISHTK"
    GLISH_INCL="$GLISH_INCL -I../../include/Rivet"
    GLISH_LIB="$GLISH_LIB -L../../rivet/rivet -lrivet"
    GLISH_TK="GLISH_TK"

    #------propagate the hostname on down--------------------------------
    ac_configure_args="$ac_configure_args --host=$ARCH"
    AC_CONFIG_SUBDIRS( rivet )
else
    XLIBSW=""
    XINCLUDES=""
    GLISH_TK=""
fi


AC_SUBST(XINCLUDES)
AC_SUBST(XLIBSW)
AC_SUBST(GLISH_FLAGS)
AC_SUBST(GLISH_INCL)
AC_SUBST(GLISH_LIB)
AC_SUBST(GLISH_TK)
AC_SUBST(GLISH_NPD)

AC_OUTPUT($ARCH/glish.options:config/glish.options.in npd/$ARCH/Makefile:npd/Makefile.in sds/$ARCH/Makefile:sds/Makefile.in editline/$ARCH/Makefile:editline/Makefile.in glish/$ARCH/Makefile:glish/Makefile.in glish/clients/$ARCH/Makefile:glish/clients/Makefile.in)
