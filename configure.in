dnl Process this file with autoconf to produce a configure script.
AC_INIT(glish)

if test -z "$GLISH_HOME"; then
   if test "x$prefix" = xNONE; then
       GLISH_HOME="`pwd`"
    else
       GLISH_HOME="$prefix"
    fi
fi
AC_SUBST(GLISH_HOME)

if test -z "$NO_AUTH" && test -d npd && test -f npd/config.h.in; then
    GLISH_FLAGS="$GLISH_FLAGS -DAUTHENTICATE"
    GLISH_NPD="GLISH_NPD"
    AC_CONFIG_HEADER(glish/config.h npd/config.h)
    if test -z "$KEYDIR"; then
        KEYDIR="$GLISH_HOME/keys"
    fi
    AC_SUBST(KEYDIR)
else
    GLISH_NPD=""
    AC_CONFIG_HEADER(glish/config.h)
fi
    
dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_CHECK_PROG(RSH,remsh,remsh,rsh)

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(libc.h sys/filio.h X11/fd.h sys/select.h sigLib.h unistd.h)
AC_CHECK_HEADERS(sys/signal.h vfork.h)
AC_CHECK_HEADERS(sys/time.h)
AC_CHECK_HEADERS(stdlib.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T
AC_CHECK_TYPE(malloc_t,void*)

dnl Checks for library functions.
AC_CHECK_LIB(bsd, main)
AC_CHECK_LIB(socket, main)
AC_CHECK_LIB(inet, main)
AC_CHECK_LIB(nsl, main)
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_FUNC_VFORK
AC_CHECK_FUNC(random,AC_DEFINE(HAVE_RANDOM))
AC_CHECK_FUNC(lrand48,AC_DEFINE(HAVE_LRAND48))
AC_CHECK_FUNC(flock,AC_DEFINE(HAVE_FLOCK))
AC_CHECK_FUNC(lockf,AC_DEFINE(HAVE_LOCKF))
AC_CHECK_FUNCS(gethostname setrlimit waitpid strdup setsockopt)
AC_CHECK_FUNCS(sigprocmask uname)

AC_SUBST(GLISH_FLAGS)
AC_SUBST(GLISH_NPD)

AC_OUTPUT(Makefile npd/Makefile sds/Makefile editline/Makefile glish/Makefile glish/clients/Makefile)
